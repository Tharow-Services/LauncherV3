; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Tantalum Launcher"
#define MyAppVersion "5.1-200+"
#define MyAppPublisher "Tharow Services"
#define MyAppURL "https://tantalum.tharow.net/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{241E2A65-B901-4352-AB35-8B88C154B5BD}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={userappdata}\.tantalum
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile=C:\Users\TherayTharow\IdeaProjects\Tantalum-Launcher\LICENSE.txt
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
;PrivilegesRequiredOverridesAllowed=dialog
OutputDir=C:\Users\TherayTharow\IdeaProjects\Tantalum-Launcher\target
OutputBaseFilename=tantalum-setup
SetupIconFile=C:\Users\TherayTharow\IdeaProjects\Tantalum-Launcher\src\main\resources\icon.ico
UninstallIconFile=C:\Users\TherayTharow\IdeaProjects\Tantalum-Launcher\src\main\resources\icon.ico
Password=tantalum
Encryption=yes
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "C:\Users\TherayTharow\IdeaProjects\Tantalum-Launcher\target\TantalumLauncher.exe";DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\TherayTharow\IdeaProjects\Tantalum-Launcher\src\main\inno\settings.json"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "{tmp}\jre.zip"; DestDir: "{app}"; Flags: deleteafterinstall external
[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\TantalumLauncher.exe";
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\TantalumLauncher.exe";
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Run]
Filename: "{sys}\tar.exe"; Parameters: "-xf .\jre.zip"; WorkingDir: "{app}"; StatusMsg: "Installing Java Runtime"; Flags: runhidden

[UninstallDelete]
Type: filesandordirs; Name: "{app}"

[Code]
var
  DownloadPage: TDownloadWizardPage;

function OnDownloadProgress(const Url, FileName: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if Progress = ProgressMax then
    Log(Format('Successfully downloaded file to {tmp}: %s', [FileName]));
  Result := True;
end;

procedure InitializeWizard;
begin
  DownloadPage := CreateDownloadPage(SetupMessage(msgWizardPreparing), SetupMessage(msgPreparingDesc), @OnDownloadProgress);

end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if CurPageID = wpReady then begin
    DownloadPage.Clear;
    DownloadPage.Add('https://corretto.aws/downloads/latest/amazon-corretto-8-x64-windows-jre.zip', 'jre.zip', '');
    DownloadPage.Show;
    try
      try
        DownloadPage.Download; // This downloads the files to {tmp}
        Result := True;
      except
        if DownloadPage.AbortedByUser then
          Log('Aborted by user.')
        else
          SuppressibleMsgBox(AddPeriod(GetExceptionMessage), mbCriticalError, MB_OK, IDOK);
        Result := False;
      end;
    finally
      DownloadPage.Hide;
    end;
  end else
    Result := True;
end;
